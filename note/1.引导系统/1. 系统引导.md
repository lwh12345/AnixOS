# bootloader
## boot 加载内核引导器
> - 开机首先执行BIOS程序，BIOS会将磁盘第一个扇区数据加载到内存的 0x7C00 位置
>
> - 为什么是 0x7C00 ?
>   
>   - 0x7c00这个地址来自Intel的第一代个人电脑芯片8088，之后的CPU为了保持兼容就一直使用这个地址
>
>   - 这个操作需要的内存最少是 32KB 。内存地址从0x0000开始编号，32KB 的内存就是 0x0000~0x7FFF
>
>   - 为了把尽量多的连续内存留给操作系统，主引导记录就被放到了内存地址的尾部。由于一个扇区是512 字节，主引导扇区本身也会产生数据，需要另外留出512字节保存
>
>   - 0x7FFF - 512 - 512 + 1 = 0x7c00，也即 32KB - 1KB
>
> - 此时CPU的运行模式为实模式，可访问 **1M** 内存
>
---

# 读取硬盘
## LBA模式
> - 磁盘中扇区从0 开始依次递增编号，不用考虑扇区所在的物理结构。其实我在描述需求时已经说出了LBA 的定义，这是一种逻辑上为扇区址的方法，全称为逻辑块地址（ Logical Block Address ）
>
> - LBA28 ，用28 位比特来描述一个扇区的地址。最大寻址范围是2 的28 次方等
于268435456 个扇区，每个扇区是512 字节，最大支持128GB
>

## LBA寄存器
> - LBA寄存器有28个bit位，LBA!ow 寄存器用来存储28 位地址的第 0～7 LBAmid 寄存器用来存储第 8～15 位，LBAhigh 寄存器存储第 16～23 位
>
> - device 寄存器，8bit，低4 位用来存储LBA 地址的第24～27 位。结合上面的三个LBA 寄存器。第4 位用来指定通道上的主盘或从盘， 0 代表主盘， 1 代表从盘。第6 位用来设置是否启用LBA 方式， 1 代表启用LBA 模式。代表启用CHS 模式。另外的两位：第5 位和第7 位是固定为1 的，称为MBS 位

## 代码操作

    mov edi, 0x1000; 读取的目标内存
    mov ecx, 2; 起始扇区
    mov bl, 4; 扇区数量

    call read_disk

    read_disk:

        ; 设置读写扇区的数量
        mov dx, 0x1f2
        mov al, bl
        out dx, al

        inc dx; 0x1f3
        mov al, cl; 起始扇区的前八位
        out dx, al

        inc dx; 0x1f4
        shr ecx, 8
        mov al, cl; 起始扇区的中八位
        out dx, al

        inc dx; 0x1f5
        shr ecx, 8
        mov al, cl; 起始扇区的高八位
        out dx, al

        inc dx; 0x1f6
        shr ecx, 8
        and cl, 0b1111; 将高四位置为 0

        mov al, 0b1110_0000;
        or al, cl
        out dx, al; 主盘 - LBA 模式

        inc dx; 0x1f7
        mov al, 0x20; 读硬盘
        out dx, al

        xor ecx, ecx; 将 ecx 清空
        mov cl, bl; 得到读写扇区的数量

    .read:
        push cx; 保存 cx
        call .waits; 等待数据准备完毕
        call .reads; 读取一个扇区
        pop cx; 恢复 cx
        loop .read

    ret

    .waits:
        mov dx, 0x1f7
        .check:
            in al, dx
            jmp $+2; nop 直接跳转到下一行
            jmp $+2; 一点点延迟
            jmp $+2
            and al, 0b1000_1000
            cmp al, 0b0000_1000
            jnz .check
        ret

    .reads:
        mov dx, 0x1f0
        mov cx, 256; 一个扇区 256 字
        .readw:
            in ax, dx
            jmp $+2; 一点点延迟
            jmp $+2
            jmp $+2
            mov [edi], ax
            add edi, 2
            loop .readw
        ret